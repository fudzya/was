apply plugin: 'groovy'

group 'ru.fudzya.was'

sourceCompatibility = 1.5

repositories {
    mavenCentral()

	flatDir {
		dirs "${rootDir.absolutePath}/libraries"
	}
}

dependencies {

    compile     gradleApi()
    compile     localGroovy()
	compile     name: 'com.ibm.ws.runtime'

	testCompile gradleTestKit()
	testCompile group: 'org.testng', name: 'testng', version: '6.9.10'
}

test {
	useTestNG()

	testLogging {
		showStandardStreams = true
	}
}

task createTestClasspath {
	def outputDir = file("$rootDir/src/test/resources")

	inputs.files sourceSets.main.runtimeClasspath
	outputs.dir  outputDir

	doLast {
		outputDir.mkdirs()
		file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
	}
}

tasks.jar.dependsOn createTestClasspath